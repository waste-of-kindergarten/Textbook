# 递归与高阶函数

Haskell没有如while或者for之类的循环，实际上，在顺序式语言中循环意味着状态的改变，这与Haskell中“变量不可变”相违背。在Haskell中，我们一般使用递归函数来完成所谓的“循环”，一方面函数本身归属于数学，容易验证其正确性；另一方面，诸多数据结构的定义都离不开递归，使用递归函数对其进行处理也变得更加自然。

## 递归函数

在之前的学习中，我们或多或少已经接触过递归函数，并且已经接触过了一个递归定义的数据类型——列表。因此本节我们将通过列表的一系列函数讲解递归函数。

### 递归函数的概念

简单来说，递归函数就是能够调用自身的函数，它通常由两部分组成：至少一个 ***基本情况(base case)*** 提供终止递归地模式匹配；以及 ***递归情况(recursive case)*** 在特定的条件下调用自身[[1]](#ref1)。

> 当然，Haskell能够创建没有终止条件的递归函数，这会导致无限递归，但这在惰性求值的条件下并非完全无意义

### 定义递归函数

为了方便读者体会递归函数的定义过程，我们会重新定义一些列表相关的内置函数。我们将使用一些特殊的标记将自定义的函数与库函数区分，以防函数名称重复导致定义覆盖。

> 提示：某些函数为了简便需要与内置的定义不完全相同

**length'** : 计算列表的长度，对应`length`函数

```haskell
-- code3.hs
-- length
length' :: [a] -> Int 
length' [] = 0
length' (x:xs) = 1 + length' xs
```

`length'`函数的基本情况是空列表，当遇到空列表时，函数停止递归，返回0；当模式匹配列表为非空时，函数属于递归情况，此时对尾部进行递归（直到递归到基本情况），并将结果增一作为返回值。

**elem'**: 判断元素是否在列表中，对应`elem`函数

```haskell
-- code3.hs
-- elem
elem' :: (Eq a) => a -> [a] -> Bool
elem' a [] = False
elem' a (x:xs) = a == x || elem' a xs
```

`elem'`函数的基本情况是接受的列表是空列表，此时形参`a`所表示的值不在列表中，返回`False`；当列表非空时，函数判断第一个值是否与`a`相等，并将计算得到的布尔值与列表尾部递归的结果做或运算作为最终的返回值。

> 补充：实际上，由于惰性求值的特性，这个递归并没有计算到基础情况时就可能已经停止，根据惰性的特点，当`||`运算符左侧参数为`True`时，可以直接得到结果是`True`而无需计算右侧参数，读者可以仿照上一章使用的trace方法追踪递归的情况

**|++|**: 将两个列表拼接称为一个列表，对应`++`函数

```haskell
-- code3.hs
-- (++)
infixr 5 |++|
(|++|) :: [a] -> [a] -> [a]
[] |++| ys = ys 
(x:xs) |++| ys = x : xs |++| ys 
```

`|++|`运算符的基础情况是第一个参数为空列表，此时直接返回第二个参数；当第一个列表不为空时，将第一个列表的尾部与第二个列表拼接，并将第一个列表的头部作为新拼接列表的头部返回。

**reverse'**: 倒置列表，对应`reverse`函数。

```haskell
-- code4.hs
-- reverse
reverse' :: [a] -> [a]
reverse' [] = []
reverse' (x:xs) = reverse' xs ++ [x] 
```

当参数为空列表时，直接返回空列表；当参数为非空列表时，将列表的尾部倒置，再将头部装入列表中拼接到右侧返回。

有关列表的递归定义的函数还有很多，如`all`,`any`,`and`,`or`等等，在此无法一一列举，读者可以查询`Data.List`库获取更多信息。

### 尾递归

考虑前面定义的`reverse'`函数，看起来这个函数定义的非常自然，实际上它翻转列表的复杂度很高` $O(n ^ 2)$ `。

我们以`reverse' [1,2,3]`为例，讨论递归的过程。

```haskell
reverse' [1,2,3]
= reverse' [2,3] ++ [1]
= (reverse' [3] ++ [2]) ++ [1]
= ((reverse' [] ++ [3]) ++ [2]) ++ [1] -- 达到基础条件
= (([] ++ [3]) ++ [2]) ++ [1] 
= ([3] ++ [2]) ++ [1]
= (3 : ([] ++ [2])) ++ [1]
= [3,2] ++ [1]
= 3 : [2] ++ [1]
= 3 : 2 : [] ++ [1]
= [3,2,1]
```

整个过程需要不断地对递归翻转的尾部进行拼接，而每次拼接所需要附加元素的时间，都是第一个列表长度的线性时间，因此总时间为 ` $\frac{1 + 2 + ... + n}{2} = \frac{(1 + n)  n}{2} \sim n^2 (n \rightarrow \infty )$ `

下面给出一个改进版本的倒置函数`reverse''`,它需要借助一个辅助函数，以避免在翻转的过程中频繁地使用拼接操作：

```haskell
-- code3.hs

reverse'' :: [a] -> [a]
reverse'' l = helper l []
    where
        helper [] ys = ys
        helper (x:xs) ys = helper xs (x:ys)
```

仍以`reverse'' [1,2,3]`为例，与前面的递归过程进行对比：

```haskell
reverse'' [1,2,3]
= helper [1,2,3] []
= helper [2,3] [1]
= helper [3] [2,1]
= helper [] [3,2,1]
= [3,2,1]
```

整个过程使用辅助函数不断地将原列表的头部附加到新列表的头部，调用附加的次数与原列表的元素数量成线性关系，因而新的倒置函数的复杂度为` $O(n)$ `。

实际上，除了在时间复杂度上`reverse''`要更优，其在空间利用率上也得到显著提高。在`reverse'`函数递归调用过程中，计算只有达到了递归的基础条件后（即终止递归）才能开始进行，计算机需要分配大量的栈空间来存储中间过程，这大大降低了空间使用效率。而`reverse''`函数使用了 ***尾递归(Tail Recursion)*** ，即递归调用作为函数所有操作中的最后一步（其他操作先于递归执行），从而避免频繁调用堆栈而增加对内存的开销。

> 提示：当我们使用普通函数而非构造器进行尾递归时，由于惰性，我们仍然需要存储大量的中间过程，但我们可以使用`($!)`或者`!`强制立即求值。
> 如阶乘函数：
> ```haskell
> -- code3.hs
> -- 普通定义
> factorial :: Int -> Int 
> factorial n = if n == 0 then 1 else n * factorial (n - 1)
> -- 尾递归优化后定义1（非惰性）
> factorial' :: Int -> Int 
> factorial' n = if n == 0 then 1 else helper n 1 
>     where 
>         helper 1 m = m 
>         helper n !m = helper (n - 1) (n * m)
> -- 尾递归优化后定义2 （非惰性）        
> factorial'' :: Int -> Int
> factorial'' n = if n == 0 then 1 else helper n 1
>     where 
>         helper 1 m = m
>         helper n m = helper (n - 1) $! (n * m) 
> ```

> 补充：在诸如Haskell的惰性语言中，尾递归并不是那么有用，我们还需要讨论一种叫做 ***守卫递归(guarded recursion)*** 的概念，在这种递归中，递归调用部分要包含在数据构造器内部（作为其参数），这允许递归过程尽可能地延迟[[2]](#ref2)。










<p id="ref1">[1] Haskell/Recursion. (2022, April 10). Wikibooks. Retrieved 05:25, March 18, 2024 from https://en.wikibooks.org/w/index.php?title=Haskell/Recursion&oldid=4046891.</p>
<p id="ref2">[2] Brent Yorgey.(2009, March 25). [Haskell-cafe] Definition of "tail recursive" wrt Folds. Retrieved March 3,2024 from https://mail.haskell.org/pipermail/haskell-cafe/2009-March/058607.html</p>



